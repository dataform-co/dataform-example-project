config {
  type: "table",
  tags: ["reporting", "daily"],
  description: "This table contains all information about customers, bringing all CRM, shopify and stripe data for individual customers into one place.",
  columns: {
    customer_id: "unique customer identifier",
    first_name: "First name of the customer",
    last_name: "Last name of the customer",
    email: "Customer email",
    country: "Customer country",
    country: "Customer country group",
    order_count: "Lifetime number of orders made by the customer",
    total_spent: "Lifetime customer spend",
  },
  assertions: {
    nonNull: ["first_name", "last_name", "customer_id", "email"],
    uniqueKey: ["customer_id"]
  },
  redshift: {
    distStyle: "key",
    distKey: "id"
  }
}

select
  customers.customer_id as id,
  customers.first_name as first_name,
  customers.last_name as last_name,
  customers.email as email,
  customers.country as country,
  ${common.countryGroup("customers.country")} as country_group,
  count(orders.order_id) as order_count,
  sum(orders.amount) as total_spent
from
  ${ref("stg_crm_customers")} customers
  left join ${ref("order_stats")} orders 
    on customers.customer_id = orders.customer_id
where
  customers.customer_id is not null
  and customers.first_name <> 'internal account'
group by
  1,2,3,4,5,6
